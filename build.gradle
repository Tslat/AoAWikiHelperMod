plugins {
    id 'net.neoforged.gradle.userdev' version '7.0.77+'
}

version = "${wikihelper_version}"
group = 'net.tslat.aoawikihelpermod'
archivesBaseName = "${mod_name}-${minecraft_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
        workingDirectory project.file('run')
        programArgument '-Dgeckolib.disable_examples=true'
    }

    client {

    }

    server {
        programArgument '--nogui'
    }
}

repositories {
    maven { // Cursemaven for Curseforge hosted dependencies. Skip official CF maven for legibility reasons
        name = "CurseMaven"
        url = "https://www.cursemaven.com"

        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        name = "Geckolib" // Geckolib
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
    }
}

dependencies {
    implementation "net.neoforged:neoforge:$project.neoforge_version"

    implementation "curse.maven:advent-of-ascension-nevermine-311054:$project.aoa_version"
    compileOnly "software.bernie.geckolib:geckolib-neoforge-${project.minecraft_version}:${project.geckolib_version}"
}

processResources {
    var replaceProperties = [
            mod_id: mod_id,
            wikihelper_version: wikihelper_version,
            mc_version: minecraft_version,
            mc_version_range: mc_version_range,
            neoforge_version: neoforge_version,
            neoforge_version_range: neoforge_version_range,
            aoa_version_range: aoa_version_range,
            loader_version_range: neoforge_loader_version_range
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', '*.mixins.json']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
